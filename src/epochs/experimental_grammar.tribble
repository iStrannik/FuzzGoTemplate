start = Template;
Template = (Text | GoAction) (Text | GoAction)*;
GoAction = CommentAction | PipelineAction | IfAction | RangeAction | TemplateAction | BlockAction | WithAction ;
CommentAction = LD "/*" Text "*/" RD ;
PipelineAction = LD ( Pipeline | VarDeclarePipeline | VarAssignPipeline ) RD ;
IfAction = LD " if " Pipeline RD Template ( LD " else " (" if " Pipeline)? RD Template )? End ;
RangeAction = LD " range " ( Pipeline | VarDeclarePipeline ) RD Template ( LD " else " RD Template)? End ;
TemplateAction = LD " template " Name (Pipeline)? RD ;
BlockAction = LD " block " Name (Pipeline)? RD ;
WithAction = LD " with " Pipeline RD Template ( LD " else " RD Template)? End ;
Pipeline = Argument ( "|" Argument )* | MethodCall | FuncCall ;
VarDeclarePipeline =  Variable (" , " Variable)? " := " Pipeline ;
VarAssignPipeline =  Variable " = " Pipeline ;
Argument = Constant | " nil " | Variable | Fields | FuncCall | ("(" Argument " ) " Fields? ) ;
Variable = "$" (Name)? ;
Fields = (Variable)? "." Name ( " ." Name)? ;
FuncCall = Name ( Argument )* ;
MethodCall = ( Variable | Fields ) ( Argument )* ;
End = LD " end " RD ;
LD = " {{" ("- ")? ;
RD = (" -")? "}} " ;
Name = Letter ( Letter | Digit )* ;
Text = Href @@ 0.3| Default @@ 0.2 | Img @@ 0.3 | Style @@ 0.2;
Style = "<style>" Template "</style>" @@ 1.0;
Img = "<img src=xx:" Template ">" @@ 1.0;
Default = "<br>" Template "</br>" @@ 1.0;
Href = AStart Template AEnd @@ 0.5 | HrefStart Template HrefEnd @@ 0.5;
AStart = "<a href=\\"#\\">" @@ 1.0;
AEnd = "</a>" @@ 1.0;
HrefStart = "<a href=\\"" @@ 1.0;
HrefEnd = "\\">abc</a>" @@ 1.0;
Constant = "CONSTANT";
Letter = /[a-zA-Z]/;
Digit = /[0-9]/ ;